do_Type_FIR
    #  
    #  macro read do_Type_FIR.sm do_Type_FIR
    #  
    #  We will read the catalog file, and check the combined S/N of FIR data points
    #  We set coo_FIR = 1 if the combined S/N over FIR 100-1160um is greater than 5
    #  
    # if(!$?1) {print 'Usage: macro read do_Type_FIR.sm do_Type_FIR\n' {} return}
    macro read read_catalog_file.sm read_catalog_file
    macro read vecSNR.sm
    set snrFIR = float(_id*0.0)
    if(is_vector(f100)) { set imas = {100} define band 100 }
    if(is_vector(f160)) { set imas = {100 160} define band 160 }
    if(is_vector(f250)) { set imas = {100 160 250} define band 250 }
    if(is_vector(f350)) { set imas = {100 160 250 350} define band 350 }
    if(is_vector(f500)) { set imas = {100 160 250 350 500} define band 500 }
    if(is_vector(f1160)){ set imas = {100 160 250 350 500 1160} define band 1160 }
    foreach imax imas {
        set snr$imax = vecSNR(f$imax,df$imax)
        set snrFIR = (snr$imax>0) ? snrFIR + (snr$imax)**2 : snrFIR
    }
    set snrFIR = sqrt(snrFIR)
    set cooFIR = int(snrFIR>=5) #<TODO># S/N ratio limit -- 5 or 10?
    set id = int(_id)
    if(is_file("coo_FIR.txt")) {!cp "coo_FIR.txt" "coo_FIR_backup.txt"}
    if(is_file("coo_FIR.log")) {!cp "coo_FIR.log" "coo_FIR_backup.log"}
    if('$band'=='100') { print "coo_FIR.txt" '%15d%15g%15d%15g\n'                     {cooFIR snrFIR id snr100} }
    if('$band'=='160') { print "coo_FIR.txt" '%15d%15g%15d%15g%15g\n'                 {cooFIR snrFIR id snr100 snr160} }
    if('$band'=='250') { print "coo_FIR.txt" '%15d%15g%15d%15g%15g%15g\n'             {cooFIR snrFIR id snr100 snr160 snr250} }
    if('$band'=='350') { print "coo_FIR.txt" '%15d%15g%15d%15g%15g%15g%15g\n'         {cooFIR snrFIR id snr100 snr160 snr250 snr350} }
    if('$band'=='500') { print "coo_FIR.txt" '%15d%15g%15d%15g%15g%15g%15g%15g\n'     {cooFIR snrFIR id snr100 snr160 snr250 snr350 snr500} }
    if('$band'=='1160'){ print "coo_FIR.txt" '%15d%15g%15d%15g%15g%15g%15g%15g%15g\n' {cooFIR snrFIR id snr100 snr160 snr250 snr350 snr500 snr1160} }
    set Type_FIR = cooFIR
    !date "+%Y-%m-%d\ %H:%M:%S" > coo_FIR.log
    !echo "Writting to \"coo_FIR.txt\"!" >> coo_FIR.log
    !echo "We have "$(sum(Type_FIR))" out of "$(dimen(Type_FIR))" sources to fit with FIR data points only." >> coo_FIR.log
    !echo "Successfully output to \"coo_FIR.txt\"!" >> coo_FIR.log
    !echo "Please use the output file \"coo_FIR.txt\" for the following SED fitting!" >> coo_FIR.log
    !cat coo_FIR.log

